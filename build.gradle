plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'io.mozmani'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.hibernate.validator:hibernate-validator'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql:42.2.26'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}

bootRun {
	systemProperties = System.properties
	systemProperty 'spring.profiles.active', findProperty('profile') ?: 'local'
}


def initLocalEnv = tasks.register('initLocalEnv') {
	doLast {
		File dockerEnvFile = project.file("${rootDir}/.env")
		if (!dockerEnvFile.exists()) {
			dockerEnvFile.write('DB_PASSWORD="' + (('0'..'9') + ('A'..'Z') + ('a'..'z')).shuffled().take(32).join() + '"')
		}
		File privatePemKey = project.file("$rootDir/private.pem")
		if (!privatePemKey.exists()) {
			exec {
				workingDir '.'
				commandLine 'openssl', 'genrsa', '-out', 'private.pem', '2048'
			}
		}
		File publicPemKey = project.file("$rootDir/public.pem")
		if (!publicPemKey.exists()) {
			exec {
				workingDir '.'
				commandLine 'openssl', 'rsa', '-in', 'private.pem', '-pubout', '-out', 'public.pem'
			}
		}
	}
}

def runLocal = tasks.register('runLocal') {
	doLast {
		exec {
			workingDir '.'
			commandLine './gradlew', 'clean', 'build'
		}
		exec {
			workingDir '.'
			commandLine './gradlew', 'initLocalEnv'
		}
		exec {
			workingDir '.'
			commandLine 'docker-compose', 'up', '--build'
		}
	}
}